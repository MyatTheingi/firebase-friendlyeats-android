// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.1.1' apply false
    id 'com.android.library' version '7.1.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.21' apply false

    // The google-services plugin is required to parse the google-services.json file
    id 'com.google.gms.google-services' version '4.3.13' apply false
    //this plugin provides a task to determine which dependencies have updates.
    // Additionally, the plugin checks for updates to Gradle itself.
    id 'com.github.ben-manes.versions' version '0.41.0'
}

allprojects {

    def isNonStable = { candidate ->
        return ['alpha', 'beta', 'rc', 'SNAPSHOT'].any { word ->
            return candidate.version.contains(word)
        }
    }

    def isBlackListed = { candidate ->
        def blacklist = [
                'androidx.browser:browser'
        ]
        return blacklist.any { word ->
            return candidate.toString().contains(word)
        }
    }

    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate) || isBlackListed(it.candidate)
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}